# ---------------------------------
# knl_hyp.gnuplot
# generated by trigen Mon Nov 21 04:48:58 2016
# (c) 2016 H. Rakotoarivelo
# ---------------------------------
reset
set terminal postscript eps enhanced color 14 size 8cm,8cm
set output "runtime_total.eps"
set key Left
set size ratio 1
set xlabel "cores"
set ylabel "iteration time (s)"
set format y "%.0f"
set logscale x 2
set grid
plot "time_knl_flat.dat"using 1:($5/1e3) with linespoint title "Intel KNL DDR4" lc rgb "#4682B4" pt 5,\
     "time_knl_hyp.dat" using 1:($5/1e3) with linespoint title "Intel KNL hyperthread"   lc rgb "#778899" pt 3,\
     "time_knl_llc.dat" using 1:($5/1e3) with linespoint title "Intel KNL MCDR"    lc rgb "#800080" pt 2,\
     "time_hsw_nor.dat" using 1:($5/1e3) with linespoint title "Intel Haswell" lc rgb "#000050" pt 1,\
     "time_hsw_hyp.dat" using 1:($5/1e3) with linespoint title "Intel Haswell hyperthread" lc rgb "#8B0000",\
     "time_nehalem.dat" using 1:($5/1e3) with linespoint title "Intel Nehalem" lc rgb "#008000"

# ---------------------------------

reset
set terminal postscript eps enhanced color size 8cm,8cm
set output "speedup_total.eps"
set size ratio 1
unset logscale x
set key left Left reverse
set ylabel "speedup"
set xlabel "cores"
set yrange [1:64]
set ytics 8

t_knl = 56519
t_hyp = 28837
t_llc = 55572
t_hsw_nor = 10573
t_hsw_hyp = 6928
t_neha = 15553

set style data histogram
set style fill pattern border
set style histogram clustered
set grid
# ---------------------------------
plot "time_nehalem.dat"  using (t_neha/$5):xticlabels(1) title "Intel Nehalem" lc -1,\
     "time_hsw_nor.dat"  using (t_hsw_nor/$5):xticlabels(1) title "Intel Haswell" lc -1,\
     "time_hsw_hyp.dat"  using (t_hsw_hyp/$5):xticlabels(1) title "Intel Haswell hyperthread" lc -1,\
     "time_knl_hyp.dat"  using (t_hyp/$5):xticlabels(1) title "Intel KNL hyperthread" lc -1,\
     "time_knl_flat.dat" using (t_knl/$5):xticlabels(1) title "Intel KNL MCDR" lc -1,\
     "time_knl_llc.dat"  using (t_llc/$5):xticlabels(1) title "Intel KNL DDR4" lc -1


# ---------------------------------
